{
 parserClass="io.github.josehsantos.hack.lang.HackParser"
  parserUtilClass="io.github.josehsantos.hack.lang.parser.HackParserUtil"

  extends="io.github.josehsantos.hack.lang.psi.impl.HackPsiElementImpl"
  implements="io.github.josehsantos.hack.lang.psi.HackPsiElement"

  psiClassPrefix="Hack"
  psiImplClassSuffix="Impl"
  psiPackage="io.github.josehsantos.hack.lang.psi"
  psiImplPackage="io.github.josehsantos.hack.lang.psi.impl"

  elementTypeHolderClass="io.github.josehsantos.hack.lang.psi.HackTypes"
  elementTypeClass="io.github.josehsantos.hack.lang.HackTokenType"
  tokenTypeClass="io.github.josehsantos.hack.lang.HackTokenType"

  //Expression Extend
  implements(".*Expression")="io.github.josehsantos.hack.lang.psi.HackExpression"
  extends(".*Expression")="io.github.josehsantos.hack.lang.psi.impl.HackExpressionImpl"

  extends(".*Statement")="Statement"
  extends(".*StatementList")="StatementList"
  extends(".*Type")="Type"

  implements("FunctionDeclaration|.*Function")="io.github.josehsantos.hack.lang.psi.HackFunction"
  extends("FunctionDeclaration|.*Function")="io.github.josehsantos.hack.lang.psi.impl.HackFunctionImpl"

  tokens=[
     T_SEMICOLON = ";"
     T_COLON = ":"
     T_COMMA = ","
     T_DOT = "."
     T_LBRACKET = "["
     T_RBRACKET = "]"
     T_LPAREN = "("
     T_RPAREN = ")"
     T_LBRACE = "{"
     T_RBRACE = "}"
     T_OR = "|"
     T_XOR = "^"
     T_AND = "&"
     T_PLUS = "+"
     T_MINUS = "-"
     T_MUL = "*"
     T_DIV = "/"
     T_EQUAL = "="
     T_MOD = "%"
     T_NEGATE = "!"
     T_NOT = "~"
     T_LESS = "<"
     T_GREATER = ">"
     T_QUEST = "?"
     T_SILENCE = "@"
     T_SINGLE_QUOTE = "'"
     T_DOUBLE_QUOTE = "\""
     T_DOLLAR_SIGN = "$"
     T_ELLIPSIS="..."
     T_BACK='`'
  ]
}

HackFile ::= T_INLINE_HTML? T_OPEN_TAG TopStatementList?

fake Statement ::=
fake StatementList ::=
fake Type ::=
fake TraitRule ::=

TopStatementList::= (
                        NamespaceStatement //OK
                       | NormalStatements //ok
                       | FunctionStatement //OK
                       | ClassStatement
                       | InterfaceStatement //OK
                       | EnumStatement //OK
                       | TraitStatement //OK
                       | TypeStatement
                       | NewTypeStatement
                       | HaltCompilerStatement //OK
                       | UseStatement //OK
                       | ConstantStatement //OK
                       )* {
                        //recoverWhile="statement_recover"
                       }


HaltCompilerStatement ::= T_HALT_COMPILER '(' ')' ';'  {pin=1}

//Namespaces
NamespaceStatement ::= T_NAMESPACE NamespaceDeclaration ';'
                     | T_NAMESPACE NamespaceDeclaration? '{'  TopStatementList '}' {pin=3}

ConstantStatement ::= ConstantDeclaration ';'

UseStatement::= T_USE (T_FUNCTION | T_CONST)? UseDeclarations ';'  {pin=1}

Identifier ::= T_STRING
        | T_XHP_ATTRIBUTE
        | T_XHP_CATEGORY
        | T_XHP_CHILDREN
        | T_XHP_REQUIRED
        | T_ENUM
        | T_WHERE
        | T_JOIN
        | T_ON
        | T_IN
        | T_EQUALS
        | T_INTO
        | T_LET
        | T_ORDERBY
        | T_ASCENDING
        | T_DESCENDING
        | T_SELECT
        | T_GROUP
        | T_BY
        | T_TRUE
        | T_FALSE

UseDeclarations::=  UseDeclaration (',' UseDeclaration)*
UseDeclaration::= NamespaceDeclaration (T_AS NameHolder)?

NameHolder::= Identifier {
    implements="io.github.josehsantos.hack.lang.psi.HackPsiNamedElement"
    extends="io.github.josehsantos.hack.lang.psi.impl.HackPsiNamedElementImpl"
}

XhpNameHolder ::= XhpIdentifier {
        implements="io.github.josehsantos.hack.lang.psi.HackPsiNamedXHPElement"
        extends="io.github.josehsantos.hack.lang.psi.impl.HackPsiNamedXHPElementImpl"
}

VariableNameHolder::= VariableIdentifier {
        implements="io.github.josehsantos.hack.lang.psi.HackPsiNamedVariableElement"
        extends="io.github.josehsantos.hack.lang.psi.impl.HackPsiNamedVariableElementImpl"
}

VariableIdentifier::= T_VARIABLE {
      implements="io.github.josehsantos.hack.lang.psi.HackVariableReference"
      extends="io.github.josehsantos.hack.lang.psi.impl.HackVariableReferenceImpl"
}


NamespaceDeclaration::= T_NS_SEPARATOR? NameHolder (T_NS_SEPARATOR NameHolder)*
NamespaceName::= T_NS_SEPARATOR? Identifier (T_NS_SEPARATOR Identifier)* {pin=2}
NamespaceString::= (T_NAMESPACE NamespaceName) | NamespaceName

ConstantAssignment::= Types? NameHolder '=' AssignmentExpressionWrapper {pin=2}
ConstantDeclaration::=  T_CONST ConstantAssignment (',' ConstantAssignment)*  {pin=1}

private InnerStatements ::= NormalStatements
                    | FunctionStatement
                    | ClassStatement
                    | InterfaceStatement
                    | TraitStatement

private NormalStatements::= BlockStatement
                | IfStatement
                | NewIfStatement
                | WhileStatement
                | DoWhileStatement
                | ForStatement
                | SwitchStatement
                | BreakStatement
                | ContinueStatement
                | ReturnStatement
                | YieldStatement
                | GlobalStatement
                | EchoStatement
                | UnsetStatement
                | ';'
                | HtmlStatement
                | ForeachStatement
                | DeclareStatement
                | TryStatement
                | ThrowStatement
                | GotoStatement
                | ExpressionStatement
                | StaticStatement
                | LabelStatement


NewIfStatement  ::=  T_IF ParenthesizedExpression ':' InnerStatements* NewElseIfStatement* NewElseStatement? T_ENDIF ';'
IfStatement     ::=  T_IF ParenthesizedExpression NormalStatements ElseIfStatement* ElseStatement?
WhileStatement  ::= T_WHILE ParenthesizedExpression  (':' InnerStatements* T_ENDWHILE ';'   | NormalStatements) {pin=1}
DoWhileStatement::= T_DO  NormalStatements T_WHILE ParenthesizedExpression ';' {pin=1}
ForStatement    ::= T_FOR '(' ExpressionList ';' ExpressionList ';' ExpressionList ')'  (':' InnerStatements* T_ENDFOR ';' | NormalStatements) {pin=1}
SwitchStatement ::= T_SWITCH ParenthesizedExpression  ('{' ';'? CaseList '}' | ':' ';'? CaseList T_ENDSWITCH ';') {pin=1}

BreakStatement      ::= T_BREAK AssignmentExpressionWrapper? ';' {pin=1}
ContinueStatement   ::= T_CONTINUE AssignmentExpressionWrapper? ';' {pin=1}
ReturnStatement     ::= T_RETURN  (AssignmentExpressionWrapper|QueryExpression|AwaitExpression)? ';' {pin=1}
ForeachStatement    ::= T_FOREACH '(' AssignmentExpressionWrapper T_AWAIT? T_AS ForeachVariable ForeachOptionalArg? ')'  (':' InnerStatements* T_ENDFOREACH ';' | NormalStatements) {pin=1}
DeclareStatement    ::= T_DECLARE '(' DeclareList ')' (':' InnerStatements* T_ENDDECLARE ';' | NormalStatements) {pin=1}

TryCatch    ::= T_CATCH '(' FullyQualifiedClassName VariableIdentifier ')' Block {pin=1}
TryFinally  ::= T_FINALLY  Block {pin=1}
TryStatement::= T_TRY Block TryCatch* TryFinally? {pin=1}

YieldStatement  ::= T_YIELD T_BREAK ';' {pin=1}
GlobalStatement ::= T_GLOBAL GlobalVarList ';' {pin=1}
StaticStatement ::= T_STATIC StaticVarList ';'{pin=1}
EchoStatement   ::= T_ECHO ExpressionList ';' {pin=1}
UnsetStatement  ::= T_UNSET '(' VariableList ')' ';' {pin=1}
HtmlStatement   ::= T_CLOSE_TAG T_INLINE_HTML? T_OPEN_TAG?
ThrowStatement  ::= T_THROW AssignmentExpressionWrapper ';'{pin=1}
GotoStatement   ::= T_GOTO Identifier ';'{pin=1}

ExpressionStatement::=  (YieldExpression
                        |YieldAssignExpression
                        |YieldListAssignExpression
                        |AwaitExpression
                        |AwaitAssignExpression
                        |AwaitListAssignExpression
                        |QueryAssignExpression
                        |AssignmentExpressionWrapper) ';'


LabelStatement   ::= NameHolder ':'
FunctionStatement::= FunctionDeclaration
EnumStatement    ::= EnumDeclaration
ClassStatement   ::= ClassDeclaration

ClassDeclaration    ::= Attributes? (T_CLASS | (T_ABSTRACT T_CLASS) | (T_FINAL T_CLASS)) (XhpNameHolder | NameHolder TypeVar? ) Extends? Implements? ClassBlock {pin=2}

EnumDeclaration     ::= Attributes? T_ENUM NameHolder  ':' Types (T_AS Types)? '{' EnumConstantStatement* '}' {pin=2}
FunctionDeclaration ::= Attributes? MemberModifier* T_FUNCTION '&'? NameHolder TypeVar?  '(' ParameterList ')' ReturnType? (Block | ';') {pin=5}

ClassBlock          ::= '{' ClassStatements* '}' {pin=1}

XhpIdentifier ::= T_XHP_LABEL
InterfaceStatement::= Attributes? T_INTERFACE NameHolder TypeVar?  InterfaceExtends? ClassBlock {pin=2}

TraitStatement::= Attributes? T_TRAIT NameHolder TypeVar?  Implements? ClassBlock {pin=2}

Extends::= T_EXTENDS FullyQualifiedClassName {pin=1}

Implements::= T_IMPLEMENTS (FullyQualifiedClassName (',' FullyQualifiedClassName)*)? {pin=1}

InterfaceExtends::= T_EXTENDS (FullyQualifiedClassName (',' FullyQualifiedClassName)*)? {pin=1}
TraitList::= (FullyQualifiedClassName (',' FullyQualifiedClassName)*)?

ForeachOptionalArg::= T_DOUBLE_ARROW ForeachVariable {pin=1}
ForeachVariable::= ('&'? MemberVariableWrapper) | ListAssignmentVariables

DeclareAssignment      ::= NameHolder '=' AssignmentExpressionWrapper {pin=2}
DeclareList            ::= (DeclareAssignment (',' DeclareAssignment )*)?

CaseBlock              ::=  (T_DEFAULT | T_CASE AssignmentExpressionWrapper) CaseSeparator InnerStatements*
CaseList               ::=  CaseBlock*
CaseSeparator          ::=  ':' | ';'

ElseIfStatement          ::= T_ELSEIF ParenthesizedExpression NormalStatements {pin=1}
NewElseIfStatement       ::= T_ELSEIF ParenthesizedExpression ':' InnerStatements* {pin=1}
ElseStatement            ::= T_ELSE NormalStatements {pin=1}
NewElseStatement         ::= T_ELSE ':' InnerStatements* {pin=1}

private ParameterList       ::= (Parameter (',' Parameter)*  ','?)?
private CallParameterList   ::= (CallParameter (',' CallParameter)* ','?)?
        Parameter           ::= Attributes? ParameterModifier* Types? ('&'|"...")? VariableNameHolder ('=' AssignmentExpressionWrapper)?
        CallParameter       ::= "..."? '&'? AssignmentExpressionWrapper

GlobalVarList           ::= (GlobalVar (',' GlobalVar)*)?
GlobalVar               ::= VariableNameHolder | ('$' MemberVariableWrapper) | (ExpressionVariable) {pin=1}
StaticVarList           ::= ((StaticAssignment|VariableNameHolder) (',' (StaticAssignment|VariableNameHolder))*)?
StaticAssignment        ::= VariableNameHolder '=' AssignmentExpressionWrapper {pin=2}

EnumConstantDeclaration ::= Types? NameHolder '=' AssignmentExpressionWrapper {pin=2}
EnumConstantStatement   ::= EnumConstantDeclaration ';'

//TODO: Inspect that rule later
ClassVariableStatement ::= (MemberModifier+ | T_VAR|MemberModifier+ Types?)  ClassVariableDeclaration

private ClassStatements::= ClassVariableStatement ';'
                            | ConstantDeclaration ';'
                            | MethodStatement ';'?
                            | XhpAttributeStatement
                            | XhpCategoryStatement
                            | XhpChildrenStatement
                            | TraitRequireDeclarationStatement
                            | TraitUseDeclarationStatement

MethodStatement ::= FunctionDeclaration {extends=FunctionStatement}
FullyQualifiedStaticIdentifier ::= FullyQualifiedClassName T_DOUBLE_COLON Identifier


TraitUseDeclarationStatement        ::= T_USE TraitList (';'| '{' (TraitPrecedenceRule |  TraitAliasRule)* '}') {pin=1}
TraitRequireDeclarationStatement    ::= T_REQUIRE (T_EXTENDS|T_IMPLEMENTS) FullyQualifiedClassName ';' {pin=1}
TraitPrecedenceRule                 ::= FullyQualifiedStaticIdentifier T_INSTEADOF TraitList ';'
TraitAliasRule                      ::= TraitAliasRuleMethod T_AS (MemberModifier* Identifier | MemberModifier+) ';'
TraitAliasRuleMethod                ::= FullyQualifiedStaticIdentifier | Identifier

XhpAttributeStatement       ::= T_XHP_ATTRIBUTE XhpAttributeDeclaration (',' XhpAttributeDeclaration)* ';' {pin=1}
XhpAttributeDeclaration     ::= XhpAttributeType XhpBareWord ((':'|'-') XhpBareWord)* ('=' AssignmentExpressionWrapper)? ('@' T_XHP_REQUIRED)? | XhpIdentifier
XhpChildrenStatement        ::= T_XHP_CHILDREN XhpChildrenDeclaration ';' {pin=1}
XhpCategoryStatement        ::= T_XHP_CATEGORY XhpCategoryDeclaration ';' {pin=1}
XhpChildrenDeclaration      ::=  XhpChildrenParenExpression | Identifier | T_EMPTY
XhpAttributeType            ::= T_ARRAY
                                | FullyQualifiedClassName
                                | T_VAR
                                | T_ENUM '{' (LiteralExpression (',' LiteralExpression)*)? '}'
                                | T_CALLABLE

XhpCategoryDeclaration      ::= T_XHP_CATEGORY_LABEL (',' T_XHP_CATEGORY_LABEL)*
XhpChildrenParenExpression  ::= '(' XhpChildrenExpression ')' ('*'|'?'|'+')?  {pin=1}
XhpChildrenExpression       ::= (XhpChildrenParenExpression | XhpChildrenTag) ((','|'|') (XhpChildrenParenExpression | XhpChildrenTag))*
XhpChildrenTag              ::= (Identifier | XhpIdentifier | T_XHP_CATEGORY_LABEL) ('*'|'?'|'+')?

Block           ::= '{' InnerStatements* '}' {pin=1}
BlockStatement  ::= Block

MemberModifier      ::=  T_PUBLIC
                       | T_PROTECTED
                       | T_PRIVATE
                       | T_STATIC
                       | T_ABSTRACT
                       | T_FINAL
                       | T_ASYNC

ParameterModifier   ::=  T_PUBLIC
                       | T_PROTECTED
                       | T_PRIVATE

ClassVariableDeclaration::=  (StaticAssignment|VariableNameHolder) (','  (StaticAssignment|VariableNameHolder))*
ExpressionList          ::= (AssignmentExpressionWrapper (',' AssignmentExpressionWrapper)*)?

YieldExpression::= T_YIELD AssignmentExpressionWrapper (T_DOUBLE_ARROW AssignmentExpressionWrapper)? {pin=1}
YieldAssignExpression::= TryVariableNameFirst '=' YieldExpression
YieldListAssignExpression::= ListAssignmentVariables '=' YieldExpression

AwaitExpression             ::= T_AWAIT AssignmentExpressionWrapper {pin=1}
AwaitAssignExpression       ::= TryVariableNameFirst '=' AwaitExpression
AwaitListAssignExpression   ::= ListAssignmentVariables '=' AwaitExpression

private ExpressionWithParentheses::= EspecialParenthesisedExpression
                            | NewExpression
                            | CloneExpression
                            | XhpExpression
                            | CollectionLiteralExpression

EspecialParenthesisedExpression ::= '(' ExpressionWithParentheses ')'{pin=2}

CollectionLiteralExpression ::= FullyQualifiedClassName '{' ArrayPairList '}'
XhpExpression               ::= XhpTag
CloneExpression             ::= T_CLONE AssignmentExpressionWrapper {pin=1}
NewExpression               ::= T_NEW AssignmentExpressionWrapper ('(' CallParameterList  ')')? {pin=1}


private AssignmentOperator ::= ('=' ('&' T_NEW)?) | T_PLUS_EQUAL | T_MINUS_EQUAL | T_MUL_EQUAL | T_DIV_EQUAL | T_CONCAT_EQUAL | T_MOD_EQUAL | T_AND_EQUAL | T_OR_EQUAL | T_XOR_EQUAL | T_SL_EQUAL | T_SR_EQUAL | T_POW_EQUAL

private PrimaryExpression::=    SuffixExpression
                              | LambdaExpression
                              | ExpressionWithParentheses
                              | ParenthesizedExpression
                              | MemberVariableWrapper
                              | ScalarExpression
                              | ArrayLiteralExpression
                              | ShapeLiteralExpression
                              | MapArrayLiteralExpression
                              | VArrayLiteralExpression
                              | BackticksExpression
                              | ClosureExpression
                              | IssetExpression
                              | EmptyExpression
                              | IncludeExpression
                              | EvalExpression
                              | RequireExpression
                              | TupleExpression
                              | ExitExpression
                              | PrintExpression

private SuffixExpression ::= MemberVariableWrapper SuffixOperator

left AssignmentExpression           ::= AssignmentOperator AssignmentExpressionWrapper{pin=1}
left TernaryExpression              ::= '?' AssignmentExpressionWrapper? ':' TernaryExpressionWrapper{pin=1}
left LogicalExpression              ::= LogicalOperator ComparativeExpressionWrapper{pin=1}
left ComparativeExpression          ::= ComparativeOperator BitwiseExpressionWrapper{pin=1}
left BitwiseExpression              ::= BitwiseOperator ShiftExpressionWrapper{pin=1}
left ShiftExpression                ::= ShiftOperator AdditiveOrConcatenationExpressionWrapper{pin=1}
left AdditiveConcatenationExpression::= AdditiveConcatenationOperator MultiplicativeExpressionWrapper{pin=1}
left MultiplicativeExpression       ::= MultiplicativeOperator InstanceofExpressionWrapper{pin=1}
left InstanceofExpression           ::= T_INSTANCEOF AssignmentExpressionWrapper{pin=1}

private PrefixExpression        ::= PrefixOperator PrefixExpressionWrapper {pin=1}
private PrefixExpressionWrapper ::= PrefixExpression  | PrimaryExpression
CastExpression                  ::= CastOperator  PrefixExpressionWrapper{pin=1}


private TryVariableNameFirst ::= VariableNameHolder|MemberVariableWrapper

private AssignmentOrListExpression              ::= AssignmentExpression | ListAssignmentExpression
private AssignmentExpressionWrapper             ::= (TryVariableNameFirst AssignmentOrListExpression+)|TernaryExpressionWrapper
private TernaryExpressionWrapper                ::= LogicalExpressionWrapper TernaryExpression?
private LogicalExpressionWrapper                ::= ComparativeExpressionWrapper LogicalExpression*
private ComparativeExpressionWrapper            ::= BitwiseExpressionWrapper ComparativeExpression*
private BitwiseExpressionWrapper                ::= ShiftExpressionWrapper BitwiseExpression*
private ShiftExpressionWrapper                  ::= AdditiveOrConcatenationExpressionWrapper ShiftExpression*
private AdditiveOrConcatenationExpressionWrapper::= MultiplicativeExpressionWrapper AdditiveConcatenationExpression*
private MultiplicativeExpressionWrapper         ::= InstanceofExpressionWrapper MultiplicativeExpression*
private InstanceofExpressionWrapper             ::= CastExpressionWrapper InstanceofExpression*
private CastExpressionWrapper                   ::= CastExpression+|PrefixExpressionWrapper

PrefixOperator::= '@' | '!' | '~' | '+' | '-' | T_INC | T_DEC
SuffixOperator::=  T_INC | T_DEC
CastOperator  ::= T_INT_CAST | T_DOUBLE_CAST | T_STRING_CAST | T_ARRAY_CAST | T_OBJECT_CAST | T_BOOL_CAST | T_UNSET_CAST

PrintExpression ::= T_PRINT AssignmentExpressionWrapper {pin=1}
ParenthesizedExpression ::= '(' AssignmentExpressionWrapper ')' {pin=1}

ListAssignmentExpression ::= ListAssignmentVariables '=' AssignmentExpressionWrapper {pin=1}
ListAssignmentVariables  ::= T_LIST '(' AssignmentList ')'

ClosureFunction                 ::= MemberModifier* T_FUNCTION '&'? '('  ParameterList ')' ReturnType? LambdaUseVariables? Block {pin=4}
LambdaFunction                  ::= T_ASYNC? ((T_LAMBDA_OP  ParameterList T_LAMBDA_CP ReturnType?)|ParameterList) LambdaBody

LambdaUseVariables                ::= T_USE '(' LexicalVarList ')' {pin=1}
ClosureExpression                 ::= ClosureFunction
LambdaExpression                  ::= LambdaFunction

LambdaBody                         ::= T_LAMBDA_ARROW (AssignmentExpressionWrapper | AwaitExpression | Block) {pin=1}
ShapeKeyName                        ::= T_CONSTANT_ENCAPSED_STRING | ClassConstant

ShapePair                          ::= ShapeKeyName T_DOUBLE_ARROW AssignmentExpressionWrapper {pin=2}
private ArrayPairList              ::= (ArrayPair (',' ArrayPair)* ','?)?

ShapeLiteralExpression            ::= T_SHAPE '(' (ShapePair (',' ShapePair)* ','?)? ')' {pin=2}
ArrayLiteralExpression            ::= T_ARRAY '(' ArrayPairList ')' | '[' ArrayPairList ']'
MapArrayLiteralExpression         ::= (T_MIARRAY|T_MSARRAY) '(' ArrayPairList ')' {pin=1}
VArrayLiteralExpression           ::= T_VARRAY '(' ArrayPairList ')' {pin=1}

QueryExpression       ::= QueryHead QueryBody
QueryAssignExpression ::= TryVariableNameFirst '=' QueryExpression
QueryHead             ::= T_FROM VariableIdentifier T_IN AssignmentExpressionWrapper
QueryBody             ::= (FromClause | LetClause | WhereClause | JoinClause | JoinIntoClause | OrderByClause)* (SelectClause | GroupClause) QueryContinuation?

FromClause          ::= T_FROM VariableIdentifier T_IN AssignmentExpressionWrapper
LetClause           ::= T_LET VariableIdentifier '=' AssignmentExpressionWrapper
WhereClause         ::= T_WHERE AssignmentExpressionWrapper
JoinClause          ::= T_JOIN VariableIdentifier T_IN AssignmentExpressionWrapper T_ON AssignmentExpressionWrapper T_EQUALS AssignmentExpressionWrapper
JoinIntoClause      ::= T_JOIN VariableIdentifier T_IN AssignmentExpressionWrapper T_ON AssignmentExpressionWrapper T_EQUALS AssignmentExpressionWrapper T_INTO VariableIdentifier
OrderByClause       ::= T_ORDERBY Ordering (',' Ordering)*

Ordering                       ::= AssignmentExpressionWrapper (T_ASCENDING | T_DESCENDING)?
SelectClause                   ::= T_SELECT AssignmentExpressionWrapper
GroupClause                    ::= T_GROUP AssignmentExpressionWrapper T_BY AssignmentExpressionWrapper
QueryContinuation              ::= T_INTO VariableIdentifier QueryBody
LexicalVarList                 ::= ('&'? VariableIdentifier (',' '&'? VariableIdentifier)* ','?)?

XhpTag                         ::= T_XHP_TAG_LT XhpIdentifier xhp_tag_body T_XHP_TAG_GT
xhp_tag_body::= (xhp_attributes? '/') | (xhp_attributes? T_XHP_TAG_GT xhp_children? T_XHP_TAG_LT '/' xhp_opt_end_label)
xhp_opt_end_label::= XhpIdentifier?
xhp_attributes::=  xhp_attribute+
xhp_attribute ::= xhp_attribute_name '=' xhp_attribute_value
xhp_children::=xhp_child+
xhp_attribute_name::= XhpIdentifier
xhp_attribute_value::= T_XHP_TEXT | '{' AssignmentExpressionWrapper '}'
xhp_child::= T_XHP_TEXT | VariableExpressionVariable | XhpTag

XhpBareWord::=   Identifier
                | T_EXIT
                | T_FUNCTION
                | T_CONST
                | T_RETURN
                | T_YIELD
                | T_AWAIT
                | T_TRY
                | T_CATCH
                | T_FINALLY
                | T_THROW
                | T_IF
                | T_ELSEIF
                | T_ENDIF
                | T_ELSE
                | T_WHILE
                | T_ENDWHILE
                | T_DO
                | T_FOR
                | T_ENDFOR
                | T_FOREACH
                | T_ENDFOREACH
                | T_DECLARE
                | T_ENDDECLARE
                | T_INSTANCEOF
                | T_AS
                | T_SWITCH
                | T_ENDSWITCH
                | T_CASE
                | T_DEFAULT
                | T_BREAK
                | T_CONTINUE
                | T_GOTO
                | T_ECHO
                | T_PRINT
                | T_CLASS
                | T_INTERFACE
                | T_EXTENDS
                | T_IMPLEMENTS
                | T_NEW
                | T_CLONE
                | T_VAR
                | T_EVAL
                | T_INCLUDE
                | T_INCLUDE_ONCE
                | T_REQUIRE
                | T_REQUIRE_ONCE
                | T_NAMESPACE
                | T_USE
                | T_GLOBAL
                | T_ISSET
                | T_EMPTY
                | T_HALT_COMPILER
                | T_STATIC
                | T_ABSTRACT
                | T_FINAL
                | T_PRIVATE
                | T_PROTECTED
                | T_PUBLIC
                | T_ASYNC
                | T_UNSET
                | T_LIST
                | T_ARRAY
                | T_LOGICAL_OR
                | T_LOGICAL_AND
                | T_LOGICAL_XOR
                | T_CLASS_C
                | T_FUNC_C
                | T_METHOD_C
                | T_LINE
                | T_FILE
                | T_DIR
                | T_NS_C
                | T_COMPILER_HALT_OFFSET
                | T_TRAIT
                | T_TRAIT_C
                | T_INSTEADOF
                | T_TYPE
                | T_NEWTYPE
                | T_SHAPE

FunctionCallExpression::= FullyQualifiedClassName '(' CallParameterList ')' {pin=2}

FullyQualifiedClassName ::= T_NAMESPACE? NamespaceName TypeArgs? | XhpIdentifier
StaticClassName         ::= FullyQualifiedClassName | T_STATIC | VariableExpression
//ClassNameReference      ::= FullyQualifiedClassName | T_STATIC | VariableNoCalls

ExitExpression ::= T_EXIT (('(' ')' ) | ParenthesizedExpression)

BackticksExpression::= '`' (T_ENCAPSED_AND_WHITESPACE | EncapsList)* '`'

LiteralExpression::=      T_LNUMBER
                        | T_DNUMBER
                        | T_ONUMBER
                        | T_CONSTANT_ENCAPSED_STRING
                        | T_LINE
                        | T_FILE
                        | T_DIR
                        | T_CLASS_C
                        | T_TRAIT_C
                        | T_METHOD_C
                        | T_FUNC_C
                        | T_NS_C
                        | T_COMPILER_HALT_OFFSET
                        | T_START_HEREDOC T_ENCAPSED_AND_WHITESPACE T_END_HEREDOC
                        | T_START_HEREDOC T_END_HEREDOC


ComparativeOperator             ::= T_IS_IDENTICAL | T_IS_NOT_IDENTICAL | T_IS_EQUAL | T_IS_NOT_EQUAL | '<' | T_IS_SMALLER_OR_EQUAL | '>' | T_IS_GREATER_OR_EQUAL
BitwiseOperator                 ::= '|' | '&' | '^'
LogicalOperator                 ::= T_BOOLEAN_OR | T_BOOLEAN_AND | T_LOGICAL_OR | T_LOGICAL_AND | T_LOGICAL_XOR
ShiftOperator                   ::= T_SL | T_SR
AdditiveConcatenationOperator   ::= '+'|'-'|'.'
MultiplicativeOperator          ::= '*' | '/' | '%' | T_POW

ScalarExpression::=   ClassConstant
                    | NamespaceString
                    | T_STRING_VARNAME
                    | LiteralExpression
                    | DoubleQuotesString
                    | SingleQuotesString
                    | HereDocString

HereDocString ::= T_START_HEREDOC EncapsList T_END_HEREDOC
SingleQuotesString ::= "'" EncapsList "'"
DoubleQuotesString ::= '"' EncapsList '"'

LiteralScalarAttribute::= T_LNUMBER
| T_DNUMBER
| T_ONUMBER
| T_CONSTANT_ENCAPSED_STRING
| T_START_HEREDOC T_ENCAPSED_AND_WHITESPACE T_END_HEREDOC
| T_START_HEREDOC T_END_HEREDOC

StaticNumericScalarAttribute::= ('+'|'-')?( T_LNUMBER
| T_DNUMBER
| T_ONUMBER
| Identifier
)

StaticScalarAttribute::= LiteralScalarAttribute
| Identifier
| StaticNumericScalarAttribute
| T_ARRAY '(' StaticArrayPairListAttribute ')'
| '[' StaticArrayPairListAttribute ']'
| T_SHAPE '(' StaticShapePairListAttribute ')'

StaticArrayPairListAttribute::= (StaticArrayItemAttribute (',' StaticArrayItemAttribute)* ','?)?
StaticArrayItemAttribute ::= StaticScalarAttribute (T_DOUBLE_ARROW StaticScalarAttribute)?
StaticShapePairListAttribute::= (StaticShapePairItem (',' StaticShapePairItem)* ','?)?
StaticShapePairItem ::= ShapeKeyName T_DOUBLE_ARROW StaticScalarAttribute
AttributeStaticScalarList::= '(' (StaticScalarAttribute (',' StaticScalarAttribute)* ','?)? ')'
Attribute ::= Identifier AttributeStaticScalarList?
Attributes::= T_SL (Attribute (',' Attribute)* ','?)? T_SR


//dimmable_variable_access_expression::=  array_access+ | ('(' ExpressionWithParentheses ')' array_access+)
//array_access::=  ('[' AssignmentExpressionWrapper ']' | '{' AssignmentExpressionWrapper '}')

//private only_member_access_wrapper ::= variable_no_calls_base member_only_access_expression*
//left member_only_access_expression ::= T_OBJECT_OPERATOR (Identifier|'{' Expression '}' | VariableExpression)

//private VariableNoCalls::= only_member_access_wrapper
//private variable_no_calls_base::= VariableExpression
//                    | dimmable_variable_access_expression
//                    | StaticClassVariableExpression
//                    | '(' ExpressionWithParentheses ')'
//                    | parenthesized_variable_expression

private possible_variable::=       FunctionCallExpression
                                 | CallableFunctionCallExpression
                                 | StaticMethodCallExpression
                                 //| dimmable_variable_access_expression
                                 | StaticClassVariableExpression
                                 | VariableExpression
                                 | parenthesized_variable_expression


private parenthesized_variable_expression ::= '(' MemberVariableWrapper ')'

private MemberVariableWrapper::=  (EspecialParenthesisedExpression|possible_variable)
                                    (DynamicVariableExpression|CallableFunctionCallExpression|MethodCallExpression|MemberVariableExpression|ArrayAccessExpression)*


left DynamicVariableExpression       ::= VariableExpressionVariable | VariableIdentifier | ExpressionVariable
left ArrayAccessExpression           ::= '[' AssignmentExpressionWrapper ']'
left MemberVariableExpression        ::= T_OBJECT_OPERATOR (Identifier|'{' AssignmentExpressionWrapper '}' | VariableExpression)
left MethodCallExpression            ::= T_OBJECT_OPERATOR ((Identifier TypeArgs?)|VariableExpression|VariableExpressionVariable) '(' CallParameterList ')' {pin=3}
left CallableFunctionCallExpression  ::=  '(' CallParameterList ')'

StaticClassVariableExpression  ::= StaticClassName T_DOUBLE_COLON VariableExpression
StaticMethodCallExpression     ::= StaticClassName T_DOUBLE_COLON ((Identifier TypeArgs?)|VariableExpression|VariableExpressionVariable) '(' CallParameterList ')'

VariableExpression          ::= '$'+ | VariableIdentifier | ExpressionVariable
VariableExpressionVariable  ::= '{' AssignmentExpressionWrapper '}'
ExpressionVariable          ::= '$' '{' AssignmentExpressionWrapper '}'

//dimmable_variable_no_calls::= ((variable_no_calls |('(' expression_with_parens')') ) property_access_without_variables) | '(' variable ')'
AssignmentList ::= ((TryVariableNameFirst | (ListAssignmentVariables)) (',' (VariableNameHolder|MemberVariableWrapper | ListAssignmentVariables))*)?
ArrayPair ::=  AssignmentExpressionWrapper (T_DOUBLE_ARROW (AssignmentExpressionWrapper|(&MemberVariableWrapper)))?

EncapsList     ::= (T_ENCAPSED_AND_WHITESPACE|EncapsVar)*
EncapsVar      ::=    EncapsIndexOffsetExpression
                    | EncapsFieldAccessExpression
                    | VariableIdentifier
                    | EncapsExpression
                    | EncapsBraceIndexOffsetExpression
                    | EncapsBraceExpression

EncapsBraceExpression                 ::= T_CURLY_OPEN MemberVariableWrapper '}' {pin=1}
EncapsBraceIndexOffsetExpression      ::= T_DOLLAR_OPEN_CURLY_BRACES T_STRING_VARNAME '[' AssignmentExpressionWrapper ']' '}'  {pin=2}
EncapsExpression                      ::= T_DOLLAR_OPEN_CURLY_BRACES AssignmentExpressionWrapper '}'  {pin=1}
EncapsFieldAccessExpression           ::= VariableIdentifier T_OBJECT_OPERATOR Identifier
EncapsIndexOffsetExpression           ::= VariableIdentifier '[' EncapsVarOffset ']' {pin=2}
EncapsVarOffset                       ::= Identifier | T_NUM_STRING | VariableIdentifier
// builtin functions expression

EvalExpression    ::= T_EVAL '(' AssignmentExpressionWrapper ')' {pin=2}
TupleExpression   ::= T_TUPLE '(' CallParameterList ')' {pin=2}
RequireExpression ::= (T_REQUIRE|T_REQUIRE_ONCE) AssignmentExpressionWrapper {pin=1}
IncludeExpression ::= (T_INCLUDE|T_INCLUDE_ONCE) AssignmentExpressionWrapper {pin=1}

EmptyExpression      ::= T_EMPTY '(' AssignmentExpressionWrapper ')' {pin=2}
IssetExpression      ::= T_ISSET '(' VariableList ')' {pin=2}
private VariableList ::= (MemberVariableWrapper (',' MemberVariableWrapper)*)?

// end builtin functions expression
ClassConstant::= StaticClassName T_DOUBLE_COLON (Identifier|T_CLASS)
NewTypeStatement ::= T_NEWTYPE NameHolder TypeVar? (T_AS Types)? '=' Types ';' {pin=1}
TypeStatement ::= T_TYPE NameHolder TypeVar? '=' Types ';' {pin=1}

TypeArgs            ::= T_TYPELIST_LT (Types (',' Types)* ','?)? T_TYPELIST_GT {pin=1}
TypeVar             ::=T_TYPELIST_LT (TypeVarDeclaration (',' TypeVarDeclaration)?)? T_TYPELIST_GT {pin=1}
TypeVarDeclaration  ::= ('+' | '-')? NameHolder (T_AS Types)?

ReturnType   ::= ':' Types {pin=1}
private Types::= NullableType
                | SoftType
                | IdentifierType
                | ArrayType
                | CallableType
                | ShapeType
                | XhpType
                | FunctionType
                | TupleType


ShapeMemberType         ::=  (T_CONSTANT_ENCAPSED_STRING | FullyQualifiedStaticIdentifier) T_DOUBLE_ARROW Types
ShapeType               ::= T_SHAPE '(' (ShapeMemberType (',' ShapeMemberType)* ','?)? ')' {pin=1}
TupleType               ::= '(' Types ',' Types (',' Types)* ','? ')' {pin=1}
FunctionType            ::= '(' T_FUNCTION '(' (Types (',' Types)* (','|"...")?)? ')' ReturnType ')' {pin=2}
XhpType                 ::= XhpIdentifier
CallableType            ::= T_CALLABLE
ArrayType               ::= T_ARRAY TypeArgs?
IdentifierType          ::= FullyQualifiedClassName
NullableType            ::= '?' Types {pin=1}
SoftType                ::= '@' Types {pin=1}